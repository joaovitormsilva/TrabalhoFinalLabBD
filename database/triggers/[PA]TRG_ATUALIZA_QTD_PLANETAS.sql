CREATE OR REPLACE TRIGGER TRG_ATUALIZA_QTD_PLANETAS FOR INSERT OR UPDATE OR DELETE ON DOMINANCIA COMPOUND TRIGGER
    -- Declaração de tipos para coleções
    TYPE T_NOMES_NACOES IS VARRAY(1000000) OF NACAO.NOME%TYPE;

    -- Declaração de variáveis
    V_NOME_NACAO_INCREMENTO NACAO.NOME%TYPE;
    V_NOME_NACAO_DECREMENTO NACAO.NOME%TYPE;
    V_NOMES_NACOES_INCREMENTO T_NOMES_NACOES := T_NOMES_NACOES();
    V_NOMES_NACOES_DECREMENTO T_NOMES_NACOES := T_NOMES_NACOES();

AFTER EACH ROW IS BEGIN
    V_NOME_NACAO_INCREMENTO := NULL;
    V_NOME_NACAO_DECREMENTO := NULL;

    -- Atribuição dos nomes das nações que terão a quantidade de planetas incrementada ou decrementada
    IF INSERTING THEN
        IF :NEW.DATA_FIM IS NULL OR :NEW.DATA_FIM > SYSDATE THEN
            V_NOME_NACAO_INCREMENTO := :NEW.NACAO;
        END IF;
    ELSIF UPDATING THEN
        IF :NEW.NACAO <> :OLD.NACAO THEN
            IF :NEW.DATA_FIM IS NULL OR :NEW.DATA_FIM > SYSDATE THEN
                V_NOME_NACAO_INCREMENTO := :NEW.NACAO;
            END IF;
            IF :OLD.DATA_FIM IS NULL OR :OLD.DATA_FIM > SYSDATE THEN
                V_NOME_NACAO_DECREMENTO := :OLD.NACAO;
            END IF;
        ELSE
            IF (:OLD.DATA_FIM IS NULL OR :OLD.DATA_FIM > SYSDATE) AND (:NEW.DATA_FIM IS NOT NULL AND :NEW.DATA_FIM <= SYSDATE) THEN
                V_NOME_NACAO_DECREMENTO := :OLD.NACAO;
            ELSIF (:OLD.DATA_FIM IS NOT NULL AND :OLD.DATA_FIM <= SYSDATE) AND (:NEW.DATA_FIM IS NULL OR :NEW.DATA_FIM > SYSDATE) THEN
                V_NOME_NACAO_INCREMENTO := :OLD.NACAO;
            END IF;
        END IF;
    ELSIF DELETING THEN
        IF :OLD.DATA_FIM IS NULL OR :OLD.DATA_FIM > SYSDATE THEN
            V_NOME_NACAO_DECREMENTO := :OLD.NACAO;
        END IF;
    END IF;

    -- Adição do nome da nação para incremento (se necessário)
    IF V_NOME_NACAO_INCREMENTO IS NOT NULL THEN
        V_NOMES_NACOES_INCREMENTO.EXTEND();
        V_NOMES_NACOES_INCREMENTO(V_NOMES_NACOES_INCREMENTO.COUNT) := V_NOME_NACAO_INCREMENTO;
    END IF;

    -- Adição do nome da naão para decremento (se necessário)
    IF V_NOME_NACAO_DECREMENTO IS NOT NULL THEN
        V_NOMES_NACOES_DECREMENTO.EXTEND();
        V_NOMES_NACOES_DECREMENTO(V_NOMES_NACOES_DECREMENTO.COUNT) := V_NOME_NACAO_DECREMENTO;
    END IF;
END AFTER EACH ROW;

AFTER STATEMENT IS BEGIN
    -- Incremento da quantidade de planetas (caso necessário)
    FOR I IN 1..V_NOMES_NACOES_INCREMENTO.COUNT LOOP
        UPDATE NACAO SET QTD_PLANETAS = QTD_PLANETAS + 1 WHERE NOME = V_NOMES_NACOES_INCREMENTO(I);
    END LOOP;

    -- Decremento da quantidade de planetas (caso necessário)
    FOR I IN 1..V_NOMES_NACOES_DECREMENTO.COUNT LOOP
        UPDATE NACAO SET QTD_PLANETAS = QTD_PLANETAS - 1 WHERE NOME = V_NOMES_NACOES_DECREMENTO(I);
    END LOOP;
END AFTER STATEMENT;

END TRG_ATUALIZA_QTD_PLANETAS;
/
